1hp5汇编语言的种类

Intel

AT&T

boot目录中含有三个汇编代码文件

Intel

​	1. bootsect.s

​	2. setup.s

AT&T

​	3. head.s                  GNU编译

原因：Intel x86系列的处理器，GNU编译器仅仅支持i386及以后

​            不支持生成运行在实模式下的程序



### 总体功能

针对80x86结构的cpu

pc打开电源，cpu自动进入实模式，bios加电自检

从ROM-BIOS中开始执行程序代码，0xFFFF0，并在物理地址0处开始初始化中断向量

将启动设备的第一个扇区（磁盘引导扇区，512字节）读入内存绝对地址0x7C00（31KB）处

将自身移动到绝对地址0x90000（576KB）处，并将启动设备后2KB字节代码（boot/setup.s）读入到0x90200处，而内核的其他部分（system模块）被读入到从0x10000开始出，因为systm模块的禅长度不会超多0x80000字节的大小（即512KB）

之后sertup会将system移动到内存开始处



bootsect.s程序

功能：是磁盘引导块程序，驻留在磁盘的第一个扇区（引导扇区，0磁道，0磁头，第1个扇区）

1. 将自身移动到内存0x90000处
2. 将setup模块从磁盘加载到内存，紧接着bootsect，即0x90200处
3. 利用bios中断0x13取磁盘参数表中当前启动引导盘的参数，在屏幕上显示“Loading system...”字符串
4. 将system模块从磁盘上加载到内存0x10000开始的地方
5. 确定根文件系统的设备号，若没有指定，则根据所保存的引导盘的每磁道扇区数判别出盘的类型和种类并保存其设备号于root_dev（引导块的0x508地址处）
6. 长跳到setup程序的开始处执行setup程序

### 代码解析

关于机器语言和高级语言

机器语言中没有变量这一个概念，都会被编译器替换成相应的地址



```
!
! SYS_SIZE is the number of clicks (16 bytes) to be loaded.
! 0x3000 is 0x30000 bytes = 196kB, more than enough for current
! versions of linux
!
SYSSIZE = 0x3000
!
!	bootsect.s		(C) 1991 Linus Torvalds
!
! bootsect.s is loaded at 0x7c00 by the bios-startup routines, and moves
! iself out of the way to address 0x90000, and jumps there.
!
! It then loads 'setup' directly after itself (0x90200), and the system
! at 0x10000, using BIOS interrupts. 
!
! NOTE! currently system is at most 8*65536 bytes long. This should be no
! problem, even in the future. I want to keep it simple. This 512 kB
! kernel size should be enough, especially as this doesn't contain the
! buffer cache as in minix
!
! The loader has been made as simple as possible, and continuos
! read errors will result in a unbreakable loop. Reboot by hand. It
! loads pretty fast by getting whole sectors at a time whenever possible.

.globl begtext, begdata, begbss, endtext, enddata, endbss ! 定义了6个全局标识符；
.text              ! 文本段
begtext:
.data               ! 数据段
begdata:
.bss                ! 未初始化数据段（Block Started by Symbol）
begbss:
.text                ! 文本段

SETUPLEN = 4				! nr of setup-sectors
                            ! setup程序的扇区数值
BOOTSEG  = 0x07c0			! original address of boot-sector
                            ! bootsect的原始地址（是段地址，以下同）
INITSEG  = 0x9000			! we move boot here - out of the way
                            ! 将bootsect移到这 -- 避开
SETUPSEG = 0x9020			! setup starts here
                            ! setup程序在这里开始
SYSSEG   = 0x1000			! system loaded at 0x10000 (65536).
                            ! system模块加载到0x10000(64KB)处
ENDSEG   = SYSSEG + SYSSIZE		! where to stop loading
                                ! 停止加载的段地址

! ROOT_DEV:	0x000 - same type of floppy as boot.
!		0x301 - first partition on first drive etc
ROOT_DEV = 0x306

entry start                 ! 告知链接程序，程序从start标号开始执行。 
start:                      ! start作用是将自身（bootsect）从目前段位置0x07c0(31K)
                            ! 移动到0x9000处（576k），共256字（512字节），然后跳转到
                            ! 移动后代码的go标号处，也即本程序的下一语句处。
	mov	ax,#BOOTSEG         ! 将ds段寄存器置为0x7c0
	mov	ds,ax
	mov	ax,#INITSEG         ! 将es段寄存器置为0x9000
	mov	es,ax
	mov	cx,#256             ! 移动计数值=256字 
	sub	si,si               ! 源地址     ds:si = 0x07c0:0x0000
	sub	di,di               ! 目的地址   es:di = 0x9000:0x0000
	rep                     ! 重复执行，直到cx = 0
	movw                    ! 移动一个字
	jmpi	go,INITSEG      ! 间接跳转（段间跳转）。这里INITSEG指出跳转到的段地址
	                        ! 从下面开始，CPU执行已移动到0x9000段处的代码
go:	mov	ax,cs               ! 将ds，es和ss都置成移动后代码所在的段处（0x9000）
	mov	ds,ax               ! 由于程序中有堆栈操作，因此必须设置堆栈
	mov	es,ax
! put stack at 0x9ff00.     ! 将堆栈指针sp指向0x9ff00（即0x9000:0xff00）处
	mov	ss,ax
	mov	sp,#0xFF00		! arbitrary value >>512

! load the setup-sectors directly after the bootblock.
! Note that 'es' is already set up.
! 在bootsect程序块后紧跟着加载setup模块的代码数据。
! 注意es已经设置好了

load_setup:
    ! 本段代码块的作用是利用BIOS终端INT 0x13将setup模块从磁盘第二个扇区
    ! 开始读到0x90200开始处，共读四个扇区。如果读出错，则复位驱动器，并重试，
    ! 没有退路。INT 0x13的使用方法如下：
    ! 读扇区：
    ! ah = 0x02 - 读磁盘扇区到内存； al = 需要读出的扇区数量
    ! ch = 磁道（柱面）的低8位；     cl = 开始扇区（0-5）位，磁道号高2位（6-7）
    ! dh = 磁头号；                 dl = 驱动器号（如果是硬盘则位7要置位）
    ! es:bx -> 指向数据缓冲区；如果出错则CF标志置位
	mov	dx,#0x0000		! drive 0, head 0
	                    ! 第一个驱动器，第一个磁头
	mov	cx,#0x0002		! sector 2, track 0
	                    ! 第一个磁道，第二个扇区开始
	mov	bx,#0x0200		! address = 512, in INITSEG
	                    ! 读到内存的位置
	mov	ax,#0x0200+SETUPLEN	! service 2, nr of sectors
		                    ! 读4个扇区到内存
	int	0x13			! read it
	jnc	ok_load_setup		! ok - continue
	mov	dx,#0x0000
	mov	ax,#0x0000		! reset the diskette
	                    ! 若失败，则重置ax,dx的值继续读
	int	0x13
	j	load_setup

ok_load_setup:

! Get disk drive parameters, specifically nr of sectors/track
! 取磁盘驱动器的参数，特别是每磁道的扇区数量
! 取磁盘驱动器参数INT 0x13调用格式和返回信息如下：
! ah = 0x08       dl = 驱动器号（如果是硬盘则要置位7为1）
! 返回信息
! 如果出错则CF置位，并且ah = 状态码
! ah = 0， al = 0,bl = 驱动器类型（AT/PS2）
! ch = 最大磁道号的低8位，cl = 每磁道最大扇区数（位0-5），最大磁道号高2位（位6-7）
! dh = 最大磁头数，dl = 驱动器数量，
! es:di -> 软驱磁盘参数表

	mov	dl,#0x00
	mov	ax,#0x0800		! AH=8 is get drive parameters
	int	0x13
	mov	ch,#0x00
	seg cs              ! 表示下一条语句的操作数在cs段寄存器所指的段中
	mov	sectors,cx      ! 保存每磁道扇区数到cs:sectors(sectors相对于cs的偏移地址)
	mov	ax,#INITSEG
	mov	es,ax           ! 因为上面取磁盘参数终端改掉了es的值，这里重新改回

! Print some inane message    ! 显示一些信息（'Loading system...'回车换行，共24个字符）

	mov	ah,#0x03		! read cursor pos
	xor	bh,bh           ! 读取光标位置
	int	0x10
	
	mov	cx,#24          ! 共24个字符
	mov	bx,#0x0007		! page 0, attribute 7 (normal)
	mov	bp,#msg1        ! 指向要显示的字符串
	mov	ax,#0x1301		! write string, move cursor
	int	0x10            ! 写字符串并移动光标

! ok, we've written the message, now
! we want to load the system (at 0x10000) ! 现在开始将system模块加载到0x10000(64K)处

	mov	ax,#SYSSEG
	mov	es,ax		    ! segment of 0x010000
	                    ! es = 存放system的段地址
	call	read_it     ! 读磁盘上system模块，es为输入参数
	call	kill_motor  ! 关闭驱动器马达，这样就可以知道驱动器的状态了。

! After that we check which root-device to use. If the device is
! defined (!= 0), nothing is done and the given device is used.
! Otherwise, either /dev/PS0 (2,28) or /dev/at0 (2,8), depending
! on the number of sectors that the BIOS reports currently.
! 此后，我们检查要使用哪个根文件系统（简称根设备）。如果已经指定了设备（！=0）
! 就直接使用给定的设备。否则就需要根据BIOS报告的每磁道扇区数来确定到底使用
! /dev/PS0 (2,28) 还是 /dev/at0 (2,8)。
! 上面一行中两个设备文件的含义：
!     在linux中软驱的主要设备号是2，次设备号 = type*4+nr,
!     其中nr为0-3分别对应软驱A,B,C或D；type是软驱的类型（2->1.2M或7->1.44M等）
!     因为7*4+0=28，所以/dev/PS0(2,28)指的是1.44M A驱动器，其设备号是0x021c
!     同理/dev/at0 (2,8)值得是1.2M A 驱动器，其设备号是0x0208。
	seg cs
	mov	ax,root_dev
	cmp	ax,#0
	jne	root_defined
	seg cs
	mov	bx,sectors
	mov	ax,#0x0208		! /dev/ps0 - 1.2Mb
	cmp	bx,#15
	je	root_defined
	mov	ax,#0x021c		! /dev/PS0 - 1.44Mb
	cmp	bx,#18
	je	root_defined
undef_root:
	jmp undef_root
root_defined:
	seg cs
	mov	root_dev,ax

! after that (everyting loaded), we jump to
! the setup-routine loaded directly after
! the bootblock:
! 至此，所有程序都加载完毕，我们就跳转到被加载在bootsetct后面的setup
! 加载在bootsetct后面的setup程序去。
	jmpi	0,SETUPSEG            ! 跳转到0x9020:0000（setup.s程序的开始处）。
	                              !!!!本程序到此就结束了!!!!

! 下面是两个子程序

! This routine loads the system at address 0x10000, making sure
! no 64kB boundaries are crossed. We try to load it as fast as
! possible, loading whole tracks whenever we can.
!
! in:	es - starting address segment (normally 0x1000)
!
! 该子程序将系统模块加载到内存地址0x10000处，并确定没有跨越64KB的内存边界。
! 我们试图尽快的进行加载，只要可能，就每次加载整条磁道的数据。
! 输入：es - 开始内存地址段值(通常是0x1000)
sread:	.word 1+SETUPLEN	! sectors read of current track
                            ! 当前磁道中已读的扇区数。开始时已经读进1扇区的引导扇区
                            ! bootsect和setup程序所占的扇区数SETUPLEN。
head:	.word 0			! current head   ! 当前磁头号。
track:	.word 0			! current track  ! 当前磁道号。

read_it:
! 测试输入的段值。从盘上读入的数据必须存放在位于内存地址64KB的边界开始处，否则进入死循环。
! 清bx寄存器，用于表示当前段内存放数据的开始位置
	mov ax,es
	test ax,#0x0fff    ! es % 0x1000
die:	
    jne die			! es must be at 64kB boundary
                    ! es值必须位于64KB地址边界
	xor bx,bx		! bx is starting address within segment
	                ! bx为段内偏移位置
rp_read:
! 判断是否已经读入全部数据，比较当前所读段是否就是系统数据末端所处的段(#ENDSEG)，如果不是就
! 跳转至下面ok1_read标号处继续读数据。否则退出子程序返回。
	mov ax,es
	cmp ax,#ENDSEG		! have we loaded all yet?
	jb ok1_read
	ret
ok1_read:
! 计算和验证当前磁道需要读取的扇区数，放在ax寄存器中。
! 根据当前磁道还未读取的扇区数以及段内数据字节开始偏移位置，计算如果全部读取这些未读扇区，
! 所读总字节数是否超过64KB段长度的限制。若会超过，则根据此次最多能读入的字节数（64KB -
! 段偏移位置），反算出此次需要读取的扇区数。
	seg cs
	mov ax,sectors       ! 取每磁道扇区数。
	sub ax,sread         ! 减去当前磁道已读扇区数
	mov cx,ax            ! cx = ax = 当前磁道未读扇区数（read_track读取的扇区数）
	shl cx,#9            ! 左移9位，cx = cx * 512字节
	add cx,bx            ! cx = cx + 段内当前偏移值（bx）
	                     !    = 此次读操作后，段内共读入的字节数。
	jnc ok2_read         ! 若没有超过64KB字节，则跳转到ok2_read处执行。
	je ok2_read
	xor ax,ax            ! 若加上此次将读磁道上所有未读扇区时会超过64KB，则计算
	sub ax,bx            ! 此时最多能读入的字节数（64KB - 段内读偏移位置），再转换
	shr ax,#9            ! 成需要读取的扇区数
ok2_read:
	call read_track
	mov cx,ax            ! cx = 该次操作已读取的扇区数。
	add ax,sread         ! 记录该扇区上已经读取的扇区数。
	seg cs
	cmp ax,sectors       ! 如果当前磁道上还有扇区未读，则跳转到ok3_read处。
	jne ok3_read
	mov ax,#1
	sub ax,head
	jne ok4_read
	inc track
ok4_read:
	mov head,ax           ! 保存当前磁头号
	xor ax,ax             ! 清空当前磁道已读扇区数
ok3_read:                    ! 读取未读完的磁道扇区
	mov sread,ax
	shl cx,#9
	add bx,cx
	jnc rp_read
	mov ax,es
	add ax,#0x1000
	mov es,ax
	xor bx,bx
	jmp rp_read

! 读当前磁道上指定开始扇区和需读扇区数的数据到es:bx处。
! int 0x13, ah=2的说明
! al - 需读扇区数； es:bx - 缓冲区开始位置
read_track:
	push ax
	push bx
	push cx
	push dx
	mov dx,track          ! 取当前磁道号
	mov cx,sread          ! 取当前磁道上已读扇区数
	inc cx                ! cl = 开始扇区数
	mov ch,dl             ! ch = 当前磁道号
	mov dx,head           ! 取当前磁头号
	mov dh,dl             ! dh = 磁头号
	mov dl,#0             ! dl = 驱动器号（为0表示当前A驱动器）
	and dx,#0x0100        ! 磁头号不大于1
	mov ah,#2             ! ah = 2,读磁盘扇区功能号
	int 0x13
	jc bad_rt             ! 若出错，则跳转至bad_rt
	pop dx
	pop cx
	pop bx
	pop ax
	ret
! 执行驱动器复位操作（磁盘中断功能号0），再跳转到read_track处重试
bad_rt:	mov ax,#0
	mov dx,#0
	int 0x13
	pop dx
	pop cx
	pop bx
	pop ax
	jmp read_track

/*
 * This procedure turns off the floppy drive motor, so
 * that we enter the kernel in a known state, and
 * don't have to worry about it later.
 */
! 这个子程序用于关闭软驱的马达，这样我们进入内核后它处于已知状态，以后也就无须担心它了
kill_motor:
	push dx
	mov dx,#0x3f2
	mov al,#0
	outb
	pop dx
	ret

sectors:
	.word 0                          ! 存放当前启动软盘每磁道的扇区数

msg1:
	.byte 13,10                      ! 回车，换行的ASCII码
	.ascii "Loading system ..."
	.byte 13,10,13,10                ! 共24个ASCII码字节

.org 508                             ! 表示下面的语句从地址508(0x1FC)开始，所以root_dev
                                     ! 在启动扇区的第508开始的2个字节中
root_dev
	.word ROOT_DEV                   ! 这里存放根文件系统所在的设备号(init/main.c)中用到
boot_flag:
	.word 0xAA55                     ! 硬盘有效标识

.text
endtext:
.data
enddata:
.bss
endbss:
```



setup.s程序

1. 利用ROM BIOS中断读取机器系统数据，并将这些数据保存到0x90000开始的地方，即覆盖了bootsect程序所在的地方，参数如下，这些参数将会被内核中的相关程序使用。
2. 将system模块从0x10000-0x8ffff整块向下移动到0x00000处
3. 建立全局描述符表以及中断向量表
4. 开启A20地址线
5. 重新设置两个中断控制芯片8259A，将硬件中断号重新设置为0x20-0x2d。
6. 设置CPU的控制寄存器CR0，进入32位保护模式并跳入head.s程序继续运行

```
!
!	setup.s		(C) 1991 Linus Torvalds
!
! setup.s is responsible for getting the system data from the BIOS,
! and putting them into the appropriate places in system memory.
! both setup.s and system has been loaded by the bootblock.
!
! This code asks the bios for memory/disk/other parameters, and
! puts them in a "safe" place: 0x90000-0x901FF, ie where the
! boot-block used to be. It is then up to the protected mode
! system to read them from there before the area is overwritten
! for buffer-blocks.
! 
! setup.s负责从BIOS中获取系统数据，并将这些数据放到系统内存适当地方。
! 此时setup.s和system已经由bootsect引导块加载到内存中。
!
! 这段代码询问bios有关内存/磁盘/其他参数，并将这些参数放到一个“安全的”
! 地方：0x90000-0x901FFF,也就是原来bootsect代码块曾经在的地方，然后
! 在被缓冲块覆盖掉之前与保护模式的system读取。
!

! NOTE! These had better be the same as in bootsect.s!
! 以下这些参数最好和bootsector.s中的相同！

INITSEG  = 0x9000	! we move boot here - out of the way
SYSSEG   = 0x1000	! system loaded at 0x10000 (65536).
SETUPSEG = 0x9020	! this is the current segment

.globl begtext, begdata, begbss, endtext, enddata, endbss
.text
begtext:
.data
begdata:
.bss
begbss:
.text

entry start
start:

! ok, the read went well so we get current cursor position and save it for
! posterity.

	mov	ax,#INITSEG	! this is done in bootsect already, but...
	mov	ds,ax
	mov	ah,#0x03	! read cursor pos
	xor	bh,bh
	int	0x10		! save it in known place, con_init fetches
	mov	[0],dx		! it from 0x90000.

! Get memory size (extended mem, kB)

	mov	ah,#0x88
	int	0x15
	mov	[2],ax

! Get video-card data:

	mov	ah,#0x0f
	int	0x10
	mov	[4],bx		! bh = display page
	mov	[6],ax		! al = video mode, ah = window width

! check for EGA/VGA and some config parameters

	mov	ah,#0x12
	mov	bl,#0x10
	int	0x10
	mov	[8],ax
	mov	[10],bx
	mov	[12],cx

! Get hd0 data

	mov	ax,#0x0000
	mov	ds,ax
	lds	si,[4*0x41]
	mov	ax,#INITSEG
	mov	es,ax
	mov	di,#0x0080
	mov	cx,#0x10
	rep
	movsb

! Get hd1 data

	mov	ax,#0x0000
	mov	ds,ax
	lds	si,[4*0x46]
	mov	ax,#INITSEG
	mov	es,ax
	mov	di,#0x0090
	mov	cx,#0x10
	rep
	movsb

! Check that there IS a hd1 :-)

	mov	ax,#0x01500
	mov	dl,#0x81
	int	0x13
	jc	no_disk1
	cmp	ah,#3
	je	is_disk1
no_disk1:
	mov	ax,#INITSEG
	mov	es,ax
	mov	di,#0x0090
	mov	cx,#0x10
	mov	ax,#0x00
	rep
	stosb
is_disk1:

! now we want to move to protected mode ...
! 现在开始要进入保护模式中了 ...

	cli			! no interrupts allowed 
                ! 关闭中断
                
! first we move the system to it's rightful place
! 首先我们将system模块移动到正确的位置

	mov	ax,#0x0000
	cld			! 'direction'=0, movs moves forward
	            ! 方向标志位DF = 0，si/di + 1或2，对于std，df = 1,si/di - 1或2u
do_move:
	mov	es,ax		! destination segment
	add	ax,#0x1000
	cmp	ax,#0x9000  ! 判断是否已经把0x8000段开始的64k代码移动完
	jz	end_move
	mov	ds,ax		! source segment  ! ds:si -> es:di
	sub	di,di
	sub	si,si
	mov 	cx,#0x8000    ! 移动0x8000字（64k字节），需要移动8次
	rep
	movsw
	jmp	do_move

! then we load the segment descriptors
! 开始加载段描述符。
! lidt指令用于加载中断描述符表(idt)寄存器，操作数是6个字节，
! 0-1字节是描述符表长度值(字节)；
! 2-5字节是描述符表的32位线性基地址(首地址)
! 开始加载段描述符。
! 开始加载段描述符。
! 开始加载段描述符。
! 开始加载段描述符。

end_move:
	mov	ax,#SETUPSEG	! right, forgot this at first. didn't work :-)
	mov	ds,ax
	lidt	idt_48		! load idt with 0,0
	lgdt	gdt_48		! load gdt with whatever appropriate

! that was painless, now we enable A20

	call	empty_8042
	mov	al,#0xD1		! command write
	out	#0x64,al
	call	empty_8042
	mov	al,#0xDF		! A20 on
	out	#0x60,al
	call	empty_8042

! well, that went ok, I hope. Now we have to reprogram the interrupts :-(
! we put them right after the intel-reserved hardware interrupts, at
! int 0x20-0x2F. There they won't mess up anything. Sadly IBM really
! messed this up with the original PC, and they haven't been able to
! rectify it afterwards. Thus the bios puts interrupts at 0x08-0x0f,
! which is used for the internal hardware interrupts as well. We just
! have to reprogram the 8259's, and it isn't fun.

	mov	al,#0x11		! initialization sequence
	out	#0x20,al		! send it to 8259A-1
	.word	0x00eb,0x00eb		! jmp $+2, jmp $+2
	out	#0xA0,al		! and to 8259A-2
	.word	0x00eb,0x00eb
	mov	al,#0x20		! start of hardware int's (0x20)
	out	#0x21,al
	.word	0x00eb,0x00eb
	mov	al,#0x28		! start of hardware int's 2 (0x28)
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0x04		! 8259-1 is master
	out	#0x21,al
	.word	0x00eb,0x00eb
	mov	al,#0x02		! 8259-2 is slave
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0x01		! 8086 mode for both
	out	#0x21,al
	.word	0x00eb,0x00eb
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0xFF		! mask off all interrupts for now
	out	#0x21,al
	.word	0x00eb,0x00eb
	out	#0xA1,al

! well, that certainly wasn't fun :-(. Hopefully it works, and we don't
! need no steenking BIOS anyway (except for the initial loading :-).
! The BIOS-routine wants lots of unnecessary data, and it's less
! "interesting" anyway. This is how REAL programmers do it.
!
! Well, now's the time to actually move into protected mode. To make
! things as simple as possible, we do no register set-up or anything,
! we let the gnu-compiled 32-bit programs do that. We just jump to
! absolute address 0x00000, in 32-bit protected mode.

	mov	ax,#0x0001	! protected mode (PE) bit         ! 保护模式比特位（PE）
	lmsw	ax		! This is it                      ! 加载机器状态字
	jmpi	0,8		! jmp offset 0 of segment 8 (cs)  ! 跳转至cs段8，偏移0处

! This routine checks that the keyboard command queue is empty
! No timeout is used - if this hangs there is something wrong with
! the machine, and we probably couldn't proceed anyway.
empty_8042:
	.word	0x00eb,0x00eb
	in	al,#0x64	! 8042 status port
	test	al,#2		! is input buffer full?
	jnz	empty_8042	! yes - loop
	ret
	
! 全局描述符表开始，描述符表由多个8个字节长的描述符项组成。
! 这里初始化了三个描述符项。第一项无用，但须存在，第二项是
! 系统代码段描述符，第三项是系统数据段描述符。
gdt:
	.word	0,0,0,0		! dummy

    ! 系统代码段选择符在gdt表中偏移量是0x08
	.word	0x07FF		! 8Mb - limit=2047 (2048*4096=8Mb)
	.word	0x0000		! base address=0
	.word	0x9A00		! code read/exec
	.word	0x00C0		! granularity=4096, 386

    ! 系统数据段段选择符在gdt表中偏移量是0x08
	.word	0x07FF		! 8Mb - limit=2047 (2048*4096=8Mb)
	.word	0x0000		! base address=0
	.word	0x9200		! data read/write
	.word	0x00C0		! granularity=4096, 386

idt_48:
	.word	0			! idt limit=0
	.word	0,0			! idt base=0L

gdt_48:
	.word	0x800		! gdt limit=2048, 256 GDT entries
	.word	512+gdt,0x9	! gdt base = 0X9xxxx
	
.text
endtext:
.data
enddata:
.bss
endbss:

```

head.s程序

1. 加载各个数据段寄存器
2. 重新设置中断描述符表idt，共256项，并使各个表项都指向一个只报错的哑中断程序
3. 重新设置gdt
4. 使用物理地址0与1M开始处的内容相比较的方法，检测A20地址线是否已真的开启
5. 测试PC机是否含有数学协处理器芯片，并在控制寄存器CR0中设置相应的标志位
6. 设置管理内存的分页处理机制，将也目录表放在绝对物理地址0开始出，紧随后面放置共可寻址16MB的4个叶表，并分别设置它们的表项
7. 利用返回指令将预先放置在堆栈中的init/main.c程序的入口地址弹出，取运行main()程序。

```
/*
 *  linux/boot/head.s
 *
 *  (C) 1991  Linus Torvalds
 */

/*
 *  head.s contains the 32-bit startup code.
 *
 * NOTE!!! Startup happens at absolute address 0x00000000, which is also where
 * the page directory will exist. The startup code will be overwritten by
 * the page directory.
 */
.text
.globl _idt,_gdt,_pg_dir,_tmp_floppy_area
_pg_dir:          #页目录表将存放在这里。
startup_32:
	movl $0x10,%eax         # $10表示请求的特权级别0（b0-1=0）,选择全局描述符表（b2=0），选择表中第2项（b3-15 = 2）
	mov %ax,%ds             # 置ds,es,fs,gs中的选择符为setup.s中构造的数据段（全局段描述符表的第二项）=0x10，并将堆栈放置在statck_start指向数组区，然后使用本程序后面定义的新中断描述符表和全局段描述表。。新全局段描述表中初始内容与setup.s中的基本一样，仅段限长从8mb修改成了16，statck_start定义在kernel/sched.c，69行。它指向user_stack数组某段的一个长指针。
	mov %ax,%es
	mov %ax,%fs
	mov %ax,%gs
	lss _stack_start,%esp
	call setup_idt
	call setup_gdt
	movl $0x10,%eax		# reload all the segment registers
	mov %ax,%ds		# after changing gdt. CS was already
	mov %ax,%es		# reloaded in 'setup_gdt'
	mov %ax,%fs
	mov %ax,%gs
	lss _stack_start,%esp
	xorl %eax,%eax
1:	incl %eax		# check that A20 really IS enabled
	movl %eax,0x000000	# loop forever if it isn't
	cmpl %eax,0x100000
	je 1b
/*
 * NOTE! 486 should set bit 16, to check for write-protect in supervisor
 * mode. Then it would be unnecessary with the "verify_area()"-calls.
 * 486 users probably want to set the NE (#5) bit also, so as to use
 * int 16 for math errors.
 */
	movl %cr0,%eax		# check math chip
	andl $0x80000011,%eax	# Save PG,PE,ET
/* "orl $0x10020,%eax" here for 486 might be good */
	orl $2,%eax		# set MP
	movl %eax,%cr0
	call check_x87
	jmp after_page_tables

/*
 * We depend on ET to be correct. This checks for 287/387.
 */
check_x87:
	fninit
	fstsw %ax
	cmpb $0,%al
	je 1f			/* no coprocessor: have to set bits */
	movl %cr0,%eax
	xorl $6,%eax		/* reset MP, set EM */
	movl %eax,%cr0
	ret
.align 2
1:	.byte 0xDB,0xE4		/* fsetpm for 287, ignored by 387 */
	ret

/*
 *  setup_idt
 *
 *  sets up a idt with 256 entries pointing to
 *  ignore_int, interrupt gates. It then loads
 *  idt. Everything that wants to install itself
 *  in the idt-table may do so themselves. Interrupts
 *  are enabled elsewhere, when we can be relatively
 *  sure everything is ok. This routine will be over-
 *  written by the page tables.
 */
 /*
 *  下面这段是设置中断描述符表子程序 setup_idt
 *
 *  将中断描述符表idt设置成具有256个项，并都指向ignore_int中断门。
 *  然后加载中断寄存器。真正实用的中断门以后再安装。当我们在其他地方认为
 *  都正常时在开启中断。该子程序会被页表覆盖掉。
 *  
 *  中断描述符表中的项虽然也是8字节组成，但其格式与全局表中的不同，被称为
 *  门描述符。它的0-1,6-7字节是偏移量，2-3字节是选择符，4-5字节是一些标志。
 *  eax存放前4字节，edx存放后4字节
 */
setup_idt:
	lea ignore_int,%edx        # 将ignore_int的有效地址（偏移值）->edx寄存器
	movl $0x00080000,%eax      # 将选择符0x0008置入eax的高16位中。
	movw %dx,%ax		/* selector = 0x0008 = cs */
	                    /* 偏移值的低16位置入eax的低16位中。此时eax含有门描述符低4字节的值 */
	movw $0x8E00,%dx	/* interrupt gate - dpl=0, present */
                        /* edx中含有门描述符高4字节的值 */
	lea _idt,%edi       # _idt是中断描述符表的地址
	mov $256,%ecx
rp_sidt:
	movl %eax,(%edi)    # 将哑中断门描述符存入表中
	movl %edx,4(%edi)
	addl $8,%edi        # edi指向表中下一项
	dec %ecx
	jne rp_sidt
	lidt idt_descr      # 加载中断描述符表寄存器
	ret

/*
 *  setup_gdt
 *
 *  This routines sets up a new gdt and loads it.
 *  Only two entries are currently built, the same
 *  ones that were built in init.s. The routine
 *  is VERY complicated at two whole lines, so this
 *  rather long comment is certainly needed :-).
 *  This routine will beoverwritten by the page tables.
 */
 /*
 *  设置全局描述符表项 setup_gdt
 *
 *  该子程序设置一个新的全局描述符表gdt，并加载。此时仅创建两个表项，
 *  与前面的一样。该子程序只有两行，“非常的”复杂，所以当前需要长的注释了。
 *  该子程序被页表覆盖掉。
 */
setup_gdt:
	lgdt gdt_descr # 加载全局描述符表寄存器。
	ret

/*
 * I put the kernel page tables right after the page directory,
 * using 4 of them to span 16 Mb of physical memory. People with
 * more than 16MB will have to expand this.
 */
 /*
 * linus将内核的内存页表直接放在页目录之后，使用了4个表来寻址16MB的物理内存
 * 如果你有多余16MB的内存，就需要在这里进行扩充修改。
 */
 
# 每个页表长为4kb字节，每个页表项需要4个字节，因此一个页表共可以存放1024个
# 表项。如果一个页表项寻址4kb的地址空间，则一个页表就可以寻址3MB的物理内存。
# 页表项的格式为：项的前0-11位存放一些标志，例如是否在内存中(p位0)，读写许可
# （R/W位1）普通用户还是超级用户使用（U/S位2），是否修改过（是否脏了）（D位6）
# 等，表项的位12-31是页框地址，用于指出一页内存的物理起始地址。

.org 0x1000    # 从偏移0x1000处开始是第一个页表（偏移0开始处将存放页表目录）
pg0:

.org 0x2000
pg1:

.org 0x3000
pg2:

.org 0x4000 
pg3:

.org 0x5000    # 定义下面的内存数据块从偏移0x5000处开始
/*
 * tmp_floppy_area is used by the floppy-driver when DMA cannot
 * reach to a buffer-block. It needs to be aligned, so that it isn't
 * on a 64kB border.
 */
 /*
 * 当DMA不能访问缓冲块时，下面的内存块就可供软盘驱动程序使用。其地址
 * 需要对其调整，这样就不会跨越64KB边界。
 */
_tmp_floppy_area:
	.fill 1024,1,0    # 共保留1024项，每项1字节，填充数值0

# 下面的这几个入栈操作用于调用/init/main.c程序和返回做准备
#
#
#
#
#
after_page_tables:
	pushl $0		# These are the parameters to main :-)
	pushl $0
	pushl $0
	pushl $L6		# return address for main, if it decides to.
	pushl $_main
	jmp setup_paging
L6:
	jmp L6			# main should never return here, but
				# just in case, we know what happens.

/* This is the default interrupt "handler" :-) */
int_msg:
	.asciz "Unknown interrupt\n\r"
.align 2
ignore_int:
	pushl %eax
	pushl %ecx
	pushl %edx
	push %ds
	push %es
	push %fs
	movl $0x10,%eax
	mov %ax,%ds
	mov %ax,%es
	mov %ax,%fs
	pushl $int_msg
	call _printk
	popl %eax
	pop %fs
	pop %es
	pop %ds
	popl %edx
	popl %ecx
	popl %eax
	iret

/*
 * Setup_paging
 *
 * This routine sets up paging by setting the page bit
 * in cr0. The page tables are set up, identity-mapping
 * the first 16MB. The pager assumes that no illegal
 * addresses are produced (ie >4Mb on a 4Mb machine).
 *
 * NOTE! Although all physical memory should be identity
 * mapped by this routine, only the kernel page functions
 * use the >1Mb addresses directly. All "normal" functions
 * use just the lower 1Mb, or the local data space, which
 * will be mapped to some other place - mm keeps track of
 * that.
 *
 * For those with more memory than 16 Mb - tough luck. I've
 * not got it, why should you :-) The source is here. Change
 * it. (Seriously - it shouldn't be too difficult. Mostly
 * change some constants etc. I left it at 16Mb, as my machine
 * even cannot be extended past that (ok, but it was cheap :-)
 * I've tried to show which constants to change by having
 * some kind of marker at them (search for "16Mb"), but I
 * won't guarantee that's all :-( )
 */
 /*
 * 这个子程序通过设置控制寄存器cr0(PG位31)来启动内存的分页处理功能，
 * 并设置各个页表项的内容，以恒等映射前16MB的物理内存。分页器假定
 * This routine sets up paging by setting the page bit
 * in cr0. The page tables are set up, identity-mapping
 * the first 16MB. The pager assumes that no illegal
 * addresses are produced (ie >4Mb on a 4Mb machine).
 *
 * NOTE! Although all physical memory should be identity
 * mapped by this routine, only the kernel page functions
 * use the >1Mb addresses directly. All "normal" functions
 * use just the lower 1Mb, or the local data space, which
 * will be mapped to some other place - mm keeps track of
 * that.
 *
 * For those with more memory than 16 Mb - tough luck. I've
 * not got it, why should you :-) The source is here. Change
 * it. (Seriously - it shouldn't be too difficult. Mostly
 * change some constants etc. I left it at 16Mb, as my machine
 * even cannot be extended past that (ok, but it was cheap :-)
 * I've tried to show which constants to change by having
 * some kind of marker at them (search for "16Mb"), but I
 * won't guarantee that's all :-( )
 */
# 在内存物理地址0x0处开始存放一页页目录表和4页页表。页目录表是系统公用的
# 而这里的4页页表则是属于内核专用。对于新的进程，系统会在主内存区为其申请
# 页面存放页表。
.align 2
setup_paging:               # 首先对5页内存（1页目录 + 4页页表）清零
	movl $1024*5,%ecx		/* 5 pages - pg_dir+4 page tables */
	xorl %eax,%eax
	xorl %edi,%edi			/* pg_dir is at 0x000 */
	cld;rep;stosl           # stosl是使用eax的值填充es：edi
	
	# 下面4句话设置页目录表中的项，因为内核共有4个页表所以只需设置4项。
	# 页目录项的结构和页表中项的结构一样，4个字节为1项。
	# $pg0+7表示：0x00001007,是页目录表中的第一项。
	# 则第一个页表所在的地址：0x000001007 & 0xfffff000 = 0x1000;
	# 则第一个页表属性标志：0x000001007 & 0x00000fff = 0x07,表示该页存在，用户可读写;
	movl $pg0+7,_pg_dir		/* set present bit/user r/w */
	movl $pg1+7,_pg_dir+4		/*  --------- " " --------- */
	movl $pg2+7,_pg_dir+8		/*  --------- " " --------- */
	movl $pg3+7,_pg_dir+12		/*  --------- " " --------- */
	
	# 下边6行填写4个页表中所有项的内容，共有：4（页表）*1024（项/页表）=4096项（0-0xfff）,
	# 也即能映射物理内存 4096 * 4kb = 16Mb。
	# 每项的内容是：当前项所映射的物理内存地址 + 该页的标志（这里均为7）
	# 使用的方法是从最后一个页表的最后一项开始倒退顺序填写。一个页表的最后一项在页表中的位置
	# 是1023*4=4092。因此最后一页的最后一项的位置就是$pg3+4092
	movl $pg3+4092,%edi     # edi -> 最后一页的最后一项
	movl $0xfff007,%eax		/*  16Mb - 4096 + 7 (r/w user,p) */
	                        # 最后一项对应物理内存页面的地址是0xfff000,
	                        # 加上属性标志7，即为0xfff007
	std                     # 方向位置位，edi值递减（4字节）
1:	stosl			        /* fill pages backwards - more efficient :-) */
	subl $0x1000,%eax       # 每填号一项，物理地址值减0x1000
	jge 1b                  # 如果小于0则说明全填写好了
	
	# 设置页目录基址寄存器cr3的值，指向页目录表
	xorl %eax,%eax		/* pg_dir is at 0x0000 */
	movl %eax,%cr3		/* cr3 - page directory start */
	movl %cr0,%eax
	orl $0x80000000,%eax
	movl %eax,%cr0		/* set paging (PG) bit */
	ret			/* this also flushes prefetch-queue */
# 在改变分页处理标志后要求使用转移指令刷新预取指令队列，这里用的是返回指令ret，
# 该返回指令的另一个作用是将堆栈中的main程序的地址弹出，并开始运行/init/main.c程序。
# 本程序到此真正结束了。

.align 2
.word 0
idt_descr:
	.word 256*8-1		# idt contains 256 entries
	.long _idt
.align 2
.word 0
gdt_descr:              # 下面两行是ldgt指令的6字节操作数：长度，基址。
	.word 256*8-1		# so does gdt (not that that's any
	.long _gdt		# magic number, but it works for me :^)

	.align 3
_idt:	.fill 256,8,0		# idt is uninitialized
                            # 256项，每项8字节，填0

# 全局表。前四项分别是空项(不用)，代码段描述符，数据段描述符，系统段描述符
# 其中系统段描述符linux没有用处。后面还预留了252项的空间，用于放置所创建任务的
# 局部描述符(LDT)和对应的任务状态段TSS的描述符。
# （0-nul,1-cs,2-ds,3-sys,4-tss0,5-ldt0,6-tss1,7-ldt1 etc...）
_gdt:	.quad 0x0000000000000000	/* NULL descriptor */
	.quad 0x00c09a0000000fff	/* 16Mb */
	.quad 0x00c0920000000fff	/* 16Mb */
	.quad 0x0000000000000000	/* TEMPORARY - don't use */
	.fill 252,8,0			/* space for LDT's and TSS's etc */
```

